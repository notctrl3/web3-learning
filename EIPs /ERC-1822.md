## [EIP-1822](https://eips.ethereum.org/EIPS/eip-1822):Universal Upgradeable Proxy Standard (UUPS)

### Motivation                            
 优化现有的代理合约技术，让开发者更容易、更安全地部署和管理可升级的智能合约。同时建立一个标准化的、更可靠的方法，来确保代理合约所使用的逻辑合约代码是正确且未被篡改的
### Specification
#### Proxy 中逻辑合约地址存储在slot`keccak256("PROXIABLE")`
#### Proxy 构造函数可以接受任意 bytes 数据，通过 delegatecall 调用逻辑合约的初始化函数（initializer），实现“模拟多种构造函数逻辑”的效果
```
contract Proxy {
    // Code position in storage is keccak256("PROXIABLE") = "0xc5f16f0fcc639fa48a6947836d9850f504798523bf8c9a3a87d5876cf622bcf7"
    constructor(bytes memory constructData, address contractLogic) public {
        // save the code address
        assembly { // solium-disable-line
            sstore(0xc5f16f0fcc639fa48a6947836d9850f504798523bf8c9a3a87d5876cf622bcf7, contractLogic)
        }
        (bool success, bytes memory _ ) = contractLogic.delegatecall(constructData); // solium-disable-line
        require(success, "Construction failed");
    }

    function() external payable {
        assembly { // solium-disable-line
            let contractLogic := sload(0xc5f16f0fcc639fa48a6947836d9850f504798523bf8c9a3a87d5876cf622bcf7)
            calldatacopy(0x0, 0x0, calldatasize)
            let success := delegatecall(sub(gas, 10000), contractLogic, 0x0, calldatasize, 0, 0)
            let retSz := returndatasize
            returndatacopy(0, 0, retSz)
            switch success
            case 0 {
                revert(0, retSz)
            }
            default {
                return(0, retSz)
            }
        }
    }
}
```
#### proxiable
新的逻辑合约需实现UUPS，
####  updateCodeAddress
存储新的逻辑合约地址到slot`keccak256("PROXIABLE")`
```
contract Proxiable {
    // Code position in storage is keccak256("PROXIABLE") = "0xc5f16f0fcc639fa48a6947836d9850f504798523bf8c9a3a87d5876cf622bcf7"

    function updateCodeAddress(address newAddress) internal {
        require(
            bytes32(0xc5f16f0fcc639fa48a6947836d9850f504798523bf8c9a3a87d5876cf622bcf7) == Proxiable(newAddress).proxiableUUID(),
            "Not compatible"
        );
        assembly { // solium-disable-line
            sstore(0xc5f16f0fcc639fa48a6947836d9850f504798523bf8c9a3a87d5876cf622bcf7, newAddress)
        }
    }
    function proxiableUUID() public pure returns (bytes32) {
        return 0xc5f16f0fcc639fa48a6947836d9850f504798523bf8c9a3a87d5876cf622bcf7;
    }
}
```
### Security Considerations
#### Storage 安全原则
- 升级逻辑合约时不要重排旧变量
- 新变量只能加在最后
- 最好用 BaseContract 保存所有变量
#### 逻辑合约安全原则
- 危险函数要权限控制或锁死
- 逻辑合约部署后尽量弃权 ownership
- 防止 SELFDESTRUCT / CALLCODE / delegatecall 被滥用
